# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test_package.gni")
import("//build/dart/dart_library.gni")
import("//build/dart/dart_test_component.gni")
import("//build/testing/host_test.gni")
import("//build/testing/host_test_data.gni")

dart_library("zircon") {
  package_name = "zircon"
  null_safe = true

  sdk_category = "partner"

  sources = [
    "src/channel.dart",
    "src/channel_reader.dart",
    "src/constants.dart",
    "src/errors.dart",
    "src/eventpair.dart",
    "src/fakes/handle.dart",
    "src/fakes/handle_disposition.dart",
    "src/fakes/handle_waiter.dart",
    "src/fakes/system.dart",
    "src/fakes/zircon_fakes.dart",
    "src/handle_wrapper.dart",
    "src/socket.dart",
    "src/socket_reader.dart",
    "src/vmo.dart",
    "zircon.dart",
  ]
}

dart_test_component("unittests_component") {
  component_name = "zircon_unittests"
  manifest = "meta/zircon_unittests.cmx"
  sources = [
    "channel_test.dart",
    "eventpair_test.dart",
    "handle_disposition_test.dart",
    "handle_test.dart",
    "socket_test.dart",
    "vmo_test.dart",
  ]
  deps = [
    ":zircon",
    "//third_party/dart-pkg/pub/test",
  ]
}

fuchsia_test_package("zircon_unittests") {
  test_components = [ ":unittests_component" ]
}

if (is_linux || is_mac) {
  host_test_data("extract_zircon_constants_data") {
    sources = [
      "${dart_sdk}/bin/dart",
      "${dart_sdk}/bin/snapshots/dartdev.dart.snapshot",
      "//zircon/system/public/zircon/errors.h",
      "//zircon/system/public/zircon/rights.h",
      "//zircon/system/public/zircon/types.h",
      "extract-zircon-constants.py",
    ]
  }

  host_test("test_extract_zircon_constants") {
    binary_path = python_exe_src
    deps = [
      ":extract_zircon_constants_data",
      "//build/python:interpreter",
    ]
    args = [
      rebase_path("extract-zircon-constants.py", root_build_dir),
      "--errors",
      rebase_path("//zircon/system/public/zircon/errors.h", root_build_dir),
      "--rights",
      rebase_path("//zircon/system/public/zircon/rights.h", root_build_dir),
      "--types",
      rebase_path("//zircon/system/public/zircon/types.h", root_build_dir),
      "--dart",
      rebase_path("${dart_sdk}/bin/dart", root_build_dir),
      "--dry-run",
    ]
  }
}

group("target_tests") {
  testonly = true
  deps = [
    ":test_extract_zircon_constants($host_toolchain)",
    ":zircon_unittests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":target_tests" ]
}
