#Copyright 2018 The Fuchsia Authors.All rights reserved.
#Use of this source code is governed by a BSD - style license that can be
#found in the LICENSE file.

import("//build/dart/dart_app.gni")
import("//build/dart/fidl_move.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

executable("test") {
  testonly = true
  output_name = "dart_inspect_vmo_test"

  sources = [ "test.cc" ]

  deps = [
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/inspect",
  ]
}

dart_app("dart_inspect_vmo_test_writer") {
  components = [
    {
      component_name = "dart_inspect_vmo_test_writer"
      component_type = "dart"
      package_root = "."
      main_dart = "lib/writer.dart"
      sources = [ "writer.dart" ]
      deps = [
        "//sdk/fidl/fuchsia.sys",
        dart_package_label.fidl,
        dart_package_label.fuchsia_inspect,
        dart_package_label.fuchsia_services,
      ]
    },
  ]

  meta = [
    {
      path = rebase_path("meta/dart_inspect_vmo_test_writer.cmx")
      dest = "dart_inspect_vmo_test_writer.cmx"
    },
  ]
}

# Run these tests using
#   fx run-test dart_inspect_vmo_test
test_package("dart_inspect_vmo_test") {
  deps = [
    ":dart_inspect_vmo_test_writer",
    ":test",
  ]

  tests = [
    {
      name = "dart_inspect_vmo_test"
      environments = basic_envs
    },
  ]
}
