# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dart/dart_library.gni")
import("//build/dart/dart_test_component.gni")
import("//build/fidl/fidl.gni")
import("//build/testing/environments.gni")

dart_library("fuchsia_component_test") {
  package_name = "fuchsia_component_test"
  null_safe = true

  sources = [
    "realm_builder.dart",
    "src/error.dart",
    "src/internal/local_component_runner.dart",
    "src/realm_builder.dart",
  ]

  deps = [
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//sdk/dart/zircon",
    "//sdk/fidl/fuchsia.component",
    "//sdk/fidl/fuchsia.component.decl",
    "//sdk/fidl/fuchsia.component.runner",
    "//sdk/fidl/fuchsia.component.test",
    "//sdk/fidl/fuchsia.io",
    "//third_party/dart-pkg/pub/collection",
  ]
}

# Run these tests using:
#   fx set ... --with //sdk/dart/fuchsia_component_test:tests
#   fx test dart_realm_builder_unittests
dart_test_component("fuchsia-component-test-dart-tests") {
  manifest = "meta/fuchsia-component-test-dart-tests.cml"

  null_safe = true

  sources = [ "dart_tests.dart" ]

  deps = [
    "//examples/components/routing/fidl:echo",
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia",
    "//sdk/dart/fuchsia_component_test",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/zircon",
    "//sdk/fidl/fuchsia.component",
    "//sdk/fidl/fuchsia.component.decl",
    "//sdk/fidl/fuchsia.component.test",
    "//sdk/fidl/fuchsia.io",
    "//sdk/fidl/fuchsia.logger",
    "//sdk/fidl/fuchsia.sys2",
    "//third_party/dart-pkg/pub/mockito",
    "//third_party/dart-pkg/pub/test",
  ]
}

fuchsia_component("dart-realm-builder-test-realm") {
  testonly = true
  manifest = "test_support/meta/dart_realm_builder_test_realm.cml"
  deps = [ "test_support:dart_realm_builder_test_realm_bin" ]
}

fuchsia_test_package("dart_realm_builder_unittests") {
  test_components = [ ":dart-realm-builder-test-realm" ]
  deps = [
    ":fuchsia-component-test-dart-tests",
    "//examples/components/realm_builder:echo_server_component",
    "//examples/components/realm_builder:echo_server_legacy_component",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":dart_realm_builder_unittests" ]

  if (dart_default_build_cfg.is_aot) {
    deps += [ "//src/dart:dart_aot_runner" ]
  } else {
    deps += [ "//src/dart:dart_jit_runner" ]
  }
}
